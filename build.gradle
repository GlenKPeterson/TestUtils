// To deploy:
// ./gradlew clean uploadArchives
// https://oss.sonatype.org/#stagingRepositories
// Close
// Release

// Deploying to OSSRH with Gradle
// https://central.sonatype.org/pages/gradle.html

// I think if you can see it here, then it's ready to be "Closed" and deployed manually:
// https://oss.sonatype.org/content/groups/staging/org/organicdesign/testUtils/
// Or maybe here:
// http://repo.maven.apache.org/maven2/org/organicdesign/testUtils/TestUtils/

plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
    id 'maven'
    id 'signing'
    id 'org.jetbrains.dokka' version '0.9.18'
    id "com.github.ben-manes.versions" version "0.21.0"
}

repositories {
    mavenLocal()
    maven {
        url = 'http://repo.maven.apache.org/maven2'
    }
}

dependencies {
    compile 'junit:junit:4.12'
    compile 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.organicdesign.indented:Indented:0.0.10'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
//    classpath "org.jetbrains.dokka:dokka-gradle-plugin"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from(sourceSets.main.allSource)
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from(javadoc.destinationDir)
}

group = 'org.organicdesign.testUtils'
archivesBaseName = 'TestUtils'
version = '0.0.12'
description = 'TestUtils'
sourceCompatibility = '1.8'

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

signing {
    useGpgCmd()
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'TestUtils'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Utilities for testing common Java contracts like equals(), hashCode(), and compareTo()'
                url 'https://github.com/GlenKPeterson/TestUtils'

                scm {
                    connection 'scm:git:https://github.com/GlenKPeterson/TestUtils.git'
                    developerConnection 'scm:git:https://github.com/GlenKPeterson/TestUtils.git'
                    url 'https://github.com/GlenKPeterson/TestUtils.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name 'Glen K. Peterson'
                        email 'glen@organicdesign.org'
                        organization 'PlanBase Inc.'
                    }
                }
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
